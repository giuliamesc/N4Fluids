///Unstructured mesh with P1 elements
string meshType = "unstructured";
string elemType = "P1";

//Remove verbosity
verbosity = 0;

//Number of refinements
int i = 7;

//Prepare output file
ofstream file ("lambda_" + meshType + "_" + elemType + ".dat");
 
//Setup domain vertices
real a = 0;
real b = pi;

//We will use the Shift-invert method to solve the eigenvalue problem A*u = lambda*B*u
//To do that, we need to specify the value of the shift
real sigma = 24;

//We also need to specify the number of eigenvalues to compute
int nev = 48;

//Prepare vectors to store the eigenvalues and eigenvectors
real[int] ev(nev);

//Build mesh
border l1(t=a,b){x=t;y=0;label=1;}
border l2(t=a,b){x=b;y=t;label=2;}
border l3(t=a,b){x=b-t;y=b;label=3;}
border l4(t=a,b){x=a;y=b-t;label=4;}
mesh Th=buildmesh(l1(2^i)+l2(2^i)+l3(2^i)+l4(2^i));
// plot(Th);

//FEspace
fespace Vh(Th, [P1,P1]);
Vh [u1, u2], [v1, v2];

//Setup problem in variational form using the Shift-invert method

varf op([u1, u2], [v1, v2]) = int2d(Th)(dx(u2)*dx(v2) - dy(u1)*dx(v2) - dx(u2)*dy(v1) + dy(u1)*dy(v1))
                            - int2d(Th)(sigma * (u1*v1 + u2*v2))
                            + on(1,2,3,4, u1=0, u2=0);

varf bb([u1, u2], [v1, v2])  = int2d(Th)(u1*v1 + u2*v2); 

//Translate into matrix problem and specify solvers
//OP = A - sigma B and we want to solve OPw = Bu
matrix OP = op(Vh, Vh, solver=Crout, factorize=1); //use Crout solver because the matrix is not positive
matrix B  = bb(Vh, Vh, solver=CG, eps=1e-20);

//Solve the eigenvalue problem and store the number of eigenvalues in k
int k = EigenValue(OP, B, sym=true, sigma=sigma, value=ev, tol=1e-10, maxit=0, ncv=0);

//Write results in a file
for (int j = 0; j < k; j++){
    file << ev[j] << endl;
}   
